plugins {
    id("java")
    id 'org.liquibase.gradle' version '2.2.0'
    id 'nu.studer.jooq' version '8.2.1'
}

group = "com.github.mattthey"
version = "1.0-SNAPSHOT"

java {
    sourceCompatibility = '17'
}

def springVersion = '3.1.3'
def lombokVersion = '1.18.28'

repositories {
    mavenLocal()
    mavenCentral()
}

// config for lombok
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

liquibase {
    activities {
         main {
            changelogFile 'src/main/resources/changelogs/init_db.sql'
            url 'jdbc:postgresql://localhost:5432/elementary'
            driver 'org.postgresql.Driver'
            username 'user'
            password 'password'
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:' + springVersion
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation "org.springframework.boot:spring-boot-starter-jooq:${springVersion}"
    implementation 'com.zaxxer:HikariCP:5.0.1'

    runtimeOnly 'org.postgresql:postgresql:42.6.0'

    liquibaseRuntime 'org.postgresql:postgresql:42.6.0'
    liquibaseRuntime 'org.liquibase:liquibase-core:4.23.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'

    jooqGenerator 'org.postgresql:postgresql:42.6.0'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation "org.springframework.boot:spring-boot-starter-test:${springVersion}"
}

tasks.named('test') {
    useJUnitPlatform()
}

jooq {
    version = '3.18.6'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/elementary'
                    user = 'user'
                    password = 'password'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = 'public.*'
                        excludes = 'Databasechangelog.*'
                    }
                    target {
                        packageName = 'com.github.mattthey.gen'
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}